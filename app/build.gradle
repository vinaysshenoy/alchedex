buildscript {

  dependencies {
    classpath "de.mannodermaus.gradle.plugins:android-junit5:$android_junit5_version"
  }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'de.mannodermaus.android-junit5'

junitPlatform {
  filters {
    engines {
      include 'spek'
    }
  }
}

//Needed for ViewHolder binding
androidExtensions {
  experimental = true
}

android {
  compileSdkVersion target_sdk
  defaultConfig {
    applicationId android_application_id
    minSdkVersion min_sdk
    targetSdkVersion target_sdk
    versionCode app_version_code
    versionName app_version_name
    javaCompileOptions {
      annotationProcessorOptions {
        includeCompileClasspath false
      }
    }
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    test.java.srcDirs += 'src/test/kotlin'
  }

  // Always show the result of every unit test, even if it passes.
  testOptions.unitTests.all {
    testLogging {
      events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
    }
  }

  testOptions {
    unitTests.returnDefaultValues = true
  }

  lintOptions {
    abortOnError true
  }
}

dependencies {
  implementation project(':data')

  implementation "com.android.support:appcompat-v7:$support_lib_version"

  // Dependencies for local unit tests
  testCompileOnly junit5()
  testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
  testImplementation("org.jetbrains.spek:spek-api:$spek_version") {
    exclude group: 'org.jetbrains.kotlin'
  }
  testImplementation("org.jetbrains.spek:spek-junit-platform-engine:$spek_version") {
    exclude group: 'org.junit.platform'
    exclude group: 'org.jetbrains.kotlin'
  }
  testImplementation "org.amshove.kluent:kluent:$kluent_version"
}
